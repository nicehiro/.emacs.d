#+title: Writing

Configuration for writing with Emacs, specifically for Markdown and Org mode.

#+begin_src emacs-lisp :exports none
  ;;; -*- lexical-binding: t -*-
#+end_src

* Org

** Basics

#+begin_src emacs-lisp
  (add-hook 'org-mode-hook 'visual-line-mode)
  (add-hook 'org-mode-hook 'hl-line-mode)

  (straight-use-package '(org-margin
                          :repo "rougier/org-margin"
                          :host github
                          :type git))
  (add-hook 'org-mode-hook 'org-margin-mode)

  ;; Set line spacing for specific modes
  (defun my-setup-line-spacing ()
    "Set line spacing."
    (setq line-spacing 0.2))

  (add-hook 'org-mode-hook 'my-setup-line-spacing)
  (add-hook 'text-mode-hook 'my-setup-line-spacing)

  ;; org downlaod
  (straight-use-package 'org-download)
  (use-package org-download
    :config
    (setq org-download-image-dir "~/Pictures/Org-Download"))
#+end_src

** Org LaTeX

For =latex= and =dvisvgm= paths on MacOS, I need to specify the full of the program. Otherwise, even the =(executable-find "latex")= shows correctly, the preview subprocess still won't find the =latex=.

#+begin_src emacs-lisp
  (require 'ox)

  (setq org-latex-compiler "pdflatex")

  ;; Configure preview options
  (setq org-format-latex-options
        '(:foreground default
          :background default
          :scale 1.5
          :html-foreground "Black"
          :html-background "Transparent"
          :html-scale 1.0
          :matchers ("begin" "$1" "$" "$$" "\\(" "\\[")))

  (setq org-preview-latex-process-alist
        '((dvisvgm :programs
                   ("/usr/local/texlive/2025/bin/universal-darwin/latex"
                    "/usr/local/texlive/2025/bin/universal-darwin/dvisvgm")
                   :description "dvi > svg"
                   :message "you need to install the programs: latex and dvisvgm."
                   :image-input-type "dvi"
                   :image-output-type "svg"
                   :image-size-adjust (1.7 . 1.5)
                   :latex-compiler
                   ("/usr/local/texlive/2025/bin/universal-darwin/latex -interaction nonstopmode -output-directory %o %f")
                   :image-converter
                   ("/usr/local/texlive/2025/bin/universal-darwin/dvisvgm %f -n -b min -c %S -o %O"))))

  (setq org-preview-latex-default-process 'dvisvgm)

  (setq org-latex-preview-ltxpng-directory "ltxpng/")
  (setq org-latex-packages-alist
        '(("" "amsmath" t)
          ("" "libertinus" t)))
#+end_src

** Org Agenda

#+begin_src emacs-lisp
  (use-package org-agenda
    :bind
    (("C-c a" . org-agenda)
     ("C-c c" . org-capture))
    :config
    ;; Directories settings
    (when (file-directory-p "~/Documents/gtd/")
      (setq org-agenda-files (list "~/Documents/gtd/")))
    (setq org-agenda-skip-deadline-if-done t)
    (setq org-agenda-skip-scheduled-if-done t)
    (setq org-agenda-start-on-weekday nil)
    (setq org-deadline-warning-days 16)
    (setq org-use-speed-commands t)
    (setq org-agenda-include-diary t)
    (setq org-agenda-start-day "+0d")
    (setq org-todo-keywords
          '(
            (sequence "TODO(t)" "STARTED(s)" "NEXT(n)" "WAITING(w)" "|" "DONE(d)")
            (sequence "|" "CANCELED(c)" "DELEGATED(l)" "SOMEDAY(f)")
            (sequence "|" "READ(r)")
            (sequence "|" "BLOG(b)")
            (sequence "|" "IDEA(i)")
            ))

    ;; my agenda view contains follow items:
    ;;; Ideas
    ;;; Day views
    ;;; Reading list
    ;;; Blogs
    (defvar org-agenda-custom-commands nil
      "Custom commands for org agenda.")
    (setq org-agenda-custom-commands
          '(("d" "Daily agenda and Ideas"
             ((todo "IDEA"
                    ((org-agenda-overriding-header "Stupid but maybe interesting IDEAs:")))
              (agenda ""
                      ((org-agenda-overriding-header "Today's agenda")
                       (org-agenda-span 'day)
                       (org-agenda-toggle-deadlines)))
              (agenda ""
                      ((org-agenda-overriding-header "Future's agenda(One week)")
                       (org-agenda-start-day "+1d")
                       (org-agenda-span 7)
                       (org-agenda-toggle-deadlines)))
              (todo "READ"
                    ((org-agenda-overriding-header "Reading List:")))
              (todo "BLOG"
                    ((org-agenda-overriding-header "Blogs:")))))))
    ;; org capture
    (defvar org-capture-templates nil
      "Org capture templates.")
    (setq org-capture-templates '(("i" "Idea"
                                   entry (file+headline "~/Documents/gtd/ideas.org" "Someday/Maybe")
                                   "* IDEA %?\nAdded: %U\n"
                                   :prepend t
                                   :kill-buffer t)
                                  ("t" "Todo"
                                   entry (file+headline "~/Documents/gtd/inbox.org" "TODOs")
                                   "* TODO %?\nAdded: %U\n"
                                   :prepend t
                                   :kill-buffer t)
                                  ("r" "Read"
                                   entry (file+headline "~/Documents/gtd/read.org" "Reading List")
                                   "* READ %?\nAdded: %U\n"
                                   :prepend t
                                   :kill-buffer t)
                                  ("b" "Blog"
                                   entry (file+headline "~/Documents/gtd/blog.org" "Blogs")
                                   "* BLOG %?\nAdded: %U\n"
                                   :prepend t
                                   :kill-buffer t))))
#+end_src

** Org Roam

#+begin_src emacs-lisp
  (straight-use-package 'org-roam)

  (use-package org-roam
    :diminish
    :bind (("C-c n a" . org-id-get-create)
           ("C-c n l" . org-roam-buffer-toggle)
           ("C-c n f" . org-roam-node-find)
           ("C-c n g" . org-roam-graph)
           ("C-c n i" . org-roam-node-insert)
           ("C-c n c" . org-roam-capture)
           ("C-c n j" . org-roam-dailies-capture-today)
           ("C-c n r" . org-roam-ref-find)
           ("C-c n R" . org-roam-ref-add)
           ("C-c n s" . org-roam-db-sync))
    :custom
    ;; (org-roam-database-connector 'sqlite-builtin)
    (org-roam-directory (expand-file-name "~/Documents/roam/"))
    (org-roam-dailies-directory (expand-file-name "~/Documents/roam/journal"))
    (org-roam-db-location "~/Documents/roam/roam.db")
    (org-roam-db-gc-threshold most-positive-fixnum)
    (with-eval-after-load "org-roam"
      (org-roam-setup)
      (org-roam-db-autosync-mode)))

  (straight-use-package 'consult-org-roam)

  (use-package consult-org-roam
    :after org-roam consult
    :init
    (require 'consult-org-roam)
    ;; Activate the minor mode
    (consult-org-roam-mode 1)
    :custom
    ;; Use `ripgrep' for searching with `consult-org-roam-search'
    (consult-org-roam-grep-func #'consult-ripgrep)
    ;; Configure a custom narrow key for `consult-buffer'
    (consult-org-roam-buffer-narrow-key ?r)
    ;; Display org-roam buffers right after non-org-roam buffers
    ;; in consult-buffer (and not down at the bottom)
    (consult-org-roam-buffer-after-buffers t)
    :config
    ;; Eventually suppress previewing for certain functions
    (consult-customize
     consult-org-roam-forward-links
     :preview-key (kbd "M-."))
    :bind
    ;; Define some convenient keybindings as an addition
    ("C-c n e" . consult-org-roam-file-find)
    ("C-c n b" . consult-org-roam-backlinks)
    ("C-c n l" . consult-org-roam-forward-links)
    ("C-c n r" . consult-org-roam-search))
#+end_src

*** Key bindings
#+tblname: org-roam-keymap-table
| Key | Command                         |
|-----+---------------------------------|
| l   | org-roam-buffer-toggle          |
| f   | org-roam-node-find              |
| g   | org-roam-graph                  |
| i   | org-roam-node-insert            |
| c   | org-roam-capture                |
| s   | org-roam-db-sync                |
| t   | org-roam-dailies-goto-today     |
| y   | org-roam-dailies-goto-yesterday |
| T   | org-roam-dailies-goto-tomorrow  |
| d   | org-roam-dailies-goto-date      |

#+header: :var org-roam-keymap-table=org-roam-keymap-table
#+begin_src emacs-lisp
  (meomacs-keymap-table org-roam-keymap org-roam-keymap-table)
  (keymap-set global-map "C-c r" 'org-roam-keymap)
#+end_src

*** Bigger GC threshold during sync.

#+begin_src emacs-lisp
  (setq org-roam-db-gc-threshold most-positive-fixnum)
#+end_src

*** Ox-Hugo export to Quartz

For more details refer to [[https://www.asterhu.com/post/20240220-publish-org-roam-with-quartz-oxhugo/][Publish org-roam notes to personal wiki using ox-hugo and Quartz]].

#+begin_src emacs-lisp
  (straight-use-package 'ox-hugo)

  (use-package ox-hugo
    :after ox
    :config
    (setq org-hugo-export-attachment t))

  (defun hiro/org-publish-to-quartz ()
    "Export current org‑roam buffer to Quartz‑compatible markdown."
    (interactive)
    (when (and (bound-and-true-p org-roam-mode)
               (org-roam-file-p))
      (let ((org-hugo-auto-export-on-save t))
        (org-hugo-export-to-md))))
  (add-hook 'org-mode-hook
            (lambda () (add-hook 'after-save-hook #'hiro/org-publish-to-quartz 0 t)))
#+end_src

After taking notes with org-roam, I use ox-hugo to export org notes to hugo-flavoured markdown files, so that Quartz can render a functional website.

There are two more thing need to config,
1. Set =org-hugo-default-section-directory= to =/= if you don't want to use =posts=.
2. Config =FrontMatter= to =quartz.config.ts= to detect =title= and other metadata.

#+begin_src emacs-lisp
  (defun my/hugo-export-org-directory (org-dir &optional recursive)
    "Export every *.org file in ORG-DIR to Markdown with ox‑hugo.

  With a prefix argument (C-u) the search is RECURSIVE, so it walks
  into sub‑directories as well.

  Each file is exported with `org-hugo-export-to-md' (whole‑file
  workflow).  You still control the output location and front‑matter
  via #+hugo_* keywords or your usual ox‑hugo variables."
    (interactive (list (read-directory-name "Org notes directory: ")
                       current-prefix-arg))
    (let* ((files (if recursive
                      (directory-files-recursively org-dir "\\.org\\'")
                    (directory-files org-dir t "\\.org\\'")))
           (org-hugo-export-front-matter-format "yaml")
           ;; don’t pop up a temporary *Org Export* buffer for every file
           (org-export-show-temporary-export-buffer nil))
      (message "ox‑hugo: exporting %d file%s …"
               (length files) (if (= (length files) 1) "" "s"))
      (dolist (f files)
        (with-current-buffer (find-file-noselect f)
          (message "  → %s" (file-name-nondirectory f))
          ;; nil nil nil nil = (async subtreep visible-only body-only)
          (org-hugo-export-to-md)          ; whole‑file export
          (save-buffer)                    ; keep any :EXPORT_ props you added
          (kill-buffer)))
      (message "ox‑hugo: done – %d file%s exported."
               (length files) (if (= (length files) 1) "" "s"))))

  ;; Optional convenience alias:
  ;; M-x hugo-export-this-directory  (recursive with C-u)
  (defalias 'hugo-export-this-directory #'my/hugo-export-org-directory)
#+End_src

Workflow:
1. Write a note
2. =hiro/org-publish-to-quartz= outputs the current roam note to Quartz
3. Use =git= to commit and push Quartz repo, and wait for deployment finish.

** Good old template shortcut

#+begin_src emacs-lisp
  (with-eval-after-load "org"
    (require 'org-tempo))
#+end_src

** Using variable pitch font

Enable ~variable-pitch-mode~.

#+begin_src emacs-lisp
  (add-hook 'org-mode-hook 'variable-pitch-mode)
#+end_src

** Turn off some eye-candy stuff

#+begin_src emacs-lisp
  (setq org-fontify-quote-and-verse-blocks nil
        org-fontify-whole-heading-line nil
        org-hide-leading-stars nil
        org-hide-leading-stars-before-indent-mode nil
        org-startup-indented nil)
#+end_src

** Babel

#+begin_src emacs-lisp
  (setq org-confirm-babel-evaluate nil)

  (with-eval-after-load "org"
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((R . t)
       (dot . t)
       (shell . t)
       (clojure . t))))

  (defun meomacs-after-babel-execute ()
    (when org-inline-image-overlays
      (org-redisplay-inline-images)))

  (add-hook 'org-babel-after-execute-hook 'meomacs-after-babel-execute)
#+end_src

* Markdown

#+begin_src emacs-lisp
  (straight-use-package 'markdown-mode)
  (straight-use-package 'edit-indirect)
#+end_src

** Keybindings

#+begin_src emacs-lisp
  (with-eval-after-load "markdown-mode"
    (define-key markdown-mode-map (kbd "C-c v") 'markdown-toggle-markup-hiding))
#+end_src

* Blog

#+begin_src emacs-lisp
  (straight-use-package 'easy-hugo)

  (use-package easy-hugo
    :init
    (setq easy-hugo-basedir "~/Documents/Projects/nicehiro.github.io/")
    (setq easy-hugo-postdir "content/posts")
    (setq easy-hugo-url "https://nicehiro.github.io")
    (setq easy-hugo-previewtime "300")
    :bind
    (("C-c C-e" . easy-hugo)
     ("C-c C-k" . easy-hugo-menu))
    :config
    (easy-hugo-enable-menu)
    (setq easy-hugo-default-ext ".org")
    (setq easy-hugo-image-directory "imgs")
    (setq easy-hugo-org-header t))
#+end_src

** Writeroom

#+begin_src emacs-lisp
  (straight-use-package 'writeroom-mode)

  (use-package writeroom-mode                 ;requires MELPA
    ;; Activate automatically in text-heavy buffers
    :hook ((text-mode   . writeroom-mode)
           (markdown-mode . writeroom-mode)
           (org-mode . writeroom-mode)
           (LaTeX-mode . writeroom-mode))
    :custom
    ;; --- Look & feel ---
    (writeroom-width 80)
    (writeroom-maximize-window nil)
    ;; --- Optional extras ---
    (writeroom-restore-window-config t)
    (writeroom-fringes-outside-margins nil)
    (writeroom-extra-line-spacing 4)
    (writeroom-mode-line t)
    (writeroom-fullscreen-effect nil))

  ;; Nice companion tweaks
  (add-hook 'writeroom-mode-enable-hook
            (lambda ()
              (visual-line-mode 1)             ;soft wrap
              (setq cursor-type 'bar)))        ;change cursor shape
  (add-hook 'writeroom-mode-disable-hook
            (lambda ()
              (visual-line-mode -1)
              (setq cursor-type t)))
#+end_src
