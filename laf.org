#+title: Look & Feel

Configuration for look and feels, make UI looks better.

#+begin_src emacs-lisp :exports none
  ;;; -*- lexical-binding: t -*-
#+end_src


* Disabling some components

No tool bar & menu bar.

#+begin_src emacs-lisp
  (tool-bar-mode -1)
  (menu-bar-mode 1)
#+end_src

No scroll bars in minibuffer.

#+begin_src emacs-lisp
  (add-hook 'after-make-frame-functions
            (lambda (frame)
              (set-window-scroll-bars
               (minibuffer-window frame) 0 nil 0 nil t)))
#+end_src

* Window divider

** GUI
#+begin_src emacs-lisp
  (window-divider-mode 0)
#+end_src

** TUI
#+begin_src emacs-lisp
  (set-display-table-slot standard-display-table
                          'vertical-border
                          (make-glyph-code ?│))
#+end_src

* Highlight Numbers
#+begin_src emacs-lisp
  (straight-use-package 'highlight-numbers)

  (require 'highlight-numbers)

  (add-hook 'prog-mode-hook 'highlight-numbers-mode)
#+end_src

* Some UI related variables

#+begin_src emacs-lisp
  (setq x-underline-at-descent-line t)
#+end_src

* Fonts

** Font Families

Default variables

#+begin_src emacs-lisp
  (defvar meomacs-font-pixelsize 16
    "Font size in pixels.")

  (defvar meomacs-fonts '((mono . "Monospace")
                          (sans . "Monospace")
                          (serif . "Monospace")
                          (cjk . "Unifont")
                          (symbol . "Unifont"))
    "Fonts to use.")
#+end_src

Load font table

** Load font table
#+begin_src emacs-lisp :var fonts=private.org:fonts
  (setq meomacs-fonts
        (mapcar (lambda (row)
                  (cons
                   (intern (car row))
                   (cadr row)))
                fonts))
#+end_src

Define functions for loading fonts.
Font families are specified in [[file:private.org::Fonts][private]] config.

#+begin_src emacs-lisp
  (defun meomacs--get-font-family (key)
    (let ((font (alist-get key meomacs-fonts)))
      (if (string-empty-p font)
          (alist-get 'mono meomacs-fonts)
        font)))

  (defun meomacs--format-font-name (family)
    (format "%s:pixelsize=%s" family meomacs-font-pixelsize))

  (defun meomacs-load-default-font ()
    "Load default font configuration."
    (let ((default-font (meomacs--format-font-name (meomacs--get-font-family 'mono))))
      (add-to-list 'default-frame-alist (cons 'font default-font))))

  (defun meomacs-load-face-font ()
    "Load face font configuration."
    (let ((sans (meomacs--get-font-family 'sans))
          (mono (meomacs--get-font-family 'mono))
          (serif (meomacs--get-font-family 'serif)))
      (set-face-attribute 'variable-pitch nil :family serif)
      (set-face-attribute 'variable-pitch-text nil :family sans)
      (set-face-attribute 'fixed-pitch nil :family mono)
      (set-face-attribute 'fixed-pitch-serif nil :family mono)))

  (defun meomacs-load-charset-font (&optional font)
    "Load charset font configuration."
    (let ((default-font (or font (meomacs--format-font-name (meomacs--get-font-family 'mono))))
          (cjk-font (meomacs--get-font-family 'cjk))
          (symbol-font (meomacs--get-font-family 'symbol)))
      (set-frame-font default-font)
      (dolist (charset '(kana han hangul cjk-misc bopomofo))
        (set-fontset-font t charset cjk-font))
      (set-fontset-font t 'symbol symbol-font)))

  (meomacs-load-default-font)
  (meomacs-load-face-font)

  ;; Run after startup
  (add-hook 'after-init-hook
            (lambda ()
              (when window-system
                (meomacs-load-charset-font))))
#+end_src

* Frame Dimensions

Set adaptive frame dimensions based on screen size.

#+begin_src emacs-lisp
  (defun meomacs-set-frame-dimensions ()
    "Set frame dimensions and center on screen based on display size."
    (let* ((display-width (display-pixel-width))
           (display-height (display-pixel-height))
           (frame-params
            (cond
             ;; Small screen (e.g., MacBook Pro 13")
             ((<= display-width 1440)
              (let ((width 100) (height 40))
                `((width . ,width) (height . ,height)
                  (left . ,(/ (- display-width (* width (frame-char-width))) 2))
                  (top . ,(/ (- display-height (* height (frame-char-height))) 2))
                  (internal-border-width . 20))))
             ;; Medium screen
             ((<= display-width 1920)
              (let ((width 140) (height 50))
                `((width . ,width) (height . ,height)
                  (left . ,(/ (- display-width (* width (frame-char-width))) 2))
                  (top . ,(/ (- display-height (* height (frame-char-height))) 2))
                  (internal-border-width . 20))))
             ;; Large screen
             (t
              (let ((width 160) (height 55))
                `((width . ,width) (height . ,height)
                  (left . ,(/ (- display-width (* width (frame-char-width))) 2))
                  (top . ,(/ (- display-height (* height (frame-char-height))) 2))
                  (internal-border-width . 20)))))))
      (dolist (param frame-params)
        (add-to-list 'default-frame-alist param))))

  ;; Apply frame dimensions
  (meomacs-set-frame-dimensions)

  ;; Reapply when creating new frames
  (add-hook 'after-make-frame-functions
            (lambda (frame)
              (meomacs-set-frame-dimensions)))
#+end_src

*** COMMENT Variants used when spliting window
#+begin_src emacs-lisp
  (defvar meomacs-font-current-variant nil)

  (defun meomacs-dynamic-set-font (&rest ignore)
    (interactive)
    (when window-system
      (when (or (frame-root-window-p (get-buffer-window))
              (frame-root-window-p (window-parent)))
      (let* ((prev-font-style meomacs-font-current-variant)
             (wl (seq-filter (lambda (w) (not (string-prefix-p " " (buffer-name (window-buffer w))))) (window-list)))
             (def (meomacs--get-font-family 'default))
             (new-variant (cond
                              ((= 1 (length wl))
                               (meomacs--get-font-family 'default))

                              ((window-combined-p)
                               (meomacs--get-font-family 'tall))

                              (t
                               (meomacs--get-font-family 'wide)))))
        (unless (equal prev-font-style new-variant)
          (setq meomacs-font-current-variant new-variant)
          (set-frame-font new-variant)
          (meomacs-load-charset-font new-variant))))))

  (setq frame-inhibit-implied-resize t)
  (add-hook 'window-state-change-hook 'meomacs-dynamic-set-font)
#+end_src

* Themes

Load themes.

#+begin_src emacs-lisp
  (straight-use-package 'ef-themes)

  (setq ef-themes-headings ; read the manual's entry or the doc string
        '((0 . (variable-pitch light 1.6))
          (1 . (variable-pitch light 1.55))
          (2 . (variable-pitch regular 1.5))
          (3 . (variable-pitch regular 1.45))
          (4 . (variable-pitch regular 1.4))
          (5 . (variable-pitch 1.35)) ; absence of weight means `bold'
          (6 . (variable-pitch 1.3))
          (7 . (variable-pitch 1.2))
          (t . (variable-pitch 1.1))))

  (setq ef-themes-mixed-fonts t
        ef-themes-variable-pitch-ui t)

  (setq ef-themes-region '(intense no-extend neutral))

  ;; Disable all other themes to avoid awkward blending:
  (mapc #'disable-theme custom-enabled-themes)

  (ef-themes-select 'ef-light)
#+end_src

Auto change light/dark themes on Mac.

#+begin_src emacs-lisp
  (defun my/apply-theme (appearance)
    "Load theme, taking current system APPEARANCE into consideration."
    (mapc #'disable-theme custom-enabled-themes)
    (pcase appearance
      ('light (ef-themes-select 'ef-light))
      ('dark (ef-themes-select 'ef-owl))))

  (add-hook 'ns-system-appearance-change-functions #'my/apply-theme)
#+end_src

One command to change ef-themes.

#+begin_src emacs-lisp
  (defun my/ef-themes-random ()
    "Randomly select an ef-theme based on system appearance."
    (interactive)
    (let* ((appearance (or (frame-parameter nil 'ns-appearance)
                           (when (boundp 'ns-system-appearance) ns-system-appearance)
                           'light))
           (themes (pcase appearance
                    ('light ef-themes-light-themes)
                    ('dark ef-themes-dark-themes)
                    (_ ef-themes-light-themes))))
      (ef-themes-select (nth (random (length themes)) themes))))
#+end_src

* Paren face
#+begin_src emacs-lisp
  (straight-use-package 'paren-face)
  (require 'paren-face)
  (setq paren-face-regexp "[][(){}]")
  (add-hook 'prog-mode-hook 'paren-face-mode)
#+end_src

* Modeline

** Add a double width space to ensure the height

#+begin_src emacs-lisp
  (defvar meomacs-increase-mode-line-height-only-once nil)

  (unless meomacs-increase-mode-line-height-only-once
    (setq-default mode-line-format
                  (append (list (propertize "﻿" 'face '(:height 1.2))) mode-line-format ))
    (setq meomacs-increase-mode-line-height-only-once t))

  ;; Set modeline border color to match background
  (defun my/set-modeline-box ()
    "Set modeline box colors to match background."
    (let ((bg-color (face-attribute 'mode-line :background))
          (inactive-bg-color (face-attribute 'mode-line-inactive :background)))
      (set-face-attribute 'mode-line nil
                          :box `(:line-width 5 :color ,bg-color))
      (set-face-attribute 'mode-line-inactive nil
                          :box `(:line-width 5 :color ,inactive-bg-color))))

  (add-hook 'ef-themes-post-load-hook #'my/set-modeline-box)
#+end_src

** Diminish

Hide unnecessary lighters.

#+begin_src emacs-lisp
  (straight-use-package 'diminish)

  (require 'diminish)
  (diminish 'gcmh-mode)
  (add-hook 'eldoc-mode-hook (lambda () (diminish 'eldoc-mode)))
  (add-hook 'hs-minor-mode-hook (lambda () (diminish 'hs-minor-mode)))
  (add-hook 'auto-revert-mode-hook (lambda () (diminish 'auto-revert-mode)))
  (add-hook 'eldoc-mode-hook (lambda () (diminish 'buffer-face-mode)))
#+end_src

** Items

#+begin_src emacs-lisp
  ;; Install doom-modeline
  (straight-use-package 'doom-modeline)

  ;; Configure doom-modeline
  (require 'doom-modeline)

  ;; Custom buffer position format: line:column
  (doom-modeline-def-segment buffer-position
    "The buffer position in format line:column."
    (format "%d:%d"
            (line-number-at-pos)
            (current-column)))

  (doom-modeline-def-modeline 'hiro/modeline
    '(modals "  " buffer-info " " remote-host " " parrot " " selection-info)
    '(misc-info minor-modes matches input-method process vcs check buffer-position "  "))

  ;; Customize doom-modeline appearance
  (setq doom-modeline-height 25)
  (setq doom-modeline-bar-width 3)
  (setq doom-modeline-window-width-limit 85)
  (setq doom-modeline-project-detection 'auto)
  (setq doom-modeline-buffer-file-name-style 'truncate-upto-project)
  (setq doom-modeline-icon t)
  (setq doom-modeline-major-mode-icon t)
  (setq doom-modeline-major-mode-color-icon t)
  (setq doom-modeline-buffer-state-icon nil)
  (setq doom-modeline-buffer-modification-icon nil)
  (setq doom-modeline-minor-modes nil)
  (setq doom-modeline-enable-word-count nil)
  (setq doom-modeline-buffer-encoding t)
  (setq doom-modeline-indent-info nil)
  (setq doom-modeline-checker-simple-format t)
  (setq doom-modeline-number-limit 99)
  (setq doom-modeline-vcs-max-length 12)
  (setq doom-modeline-workspace-name t)
  (setq doom-modeline-persp-name t)
  (setq doom-modeline-display-default-persp-name nil)
  (setq doom-modeline-persp-icon t)
  (setq doom-modeline-lsp t)
  (setq doom-modeline-github nil)
  (setq doom-modeline-github-interval 1800)
  (setq doom-modeline-env-version t)
  (setq doom-modeline-env-enable-python t)
  (setq doom-modeline-env-enable-ruby t)
  (setq doom-modeline-env-enable-perl t)
  (setq doom-modeline-env-enable-go t)
  (setq doom-modeline-env-enable-elixir t)
  (setq doom-modeline-env-enable-rust t)
  (setq doom-modeline-modal t)
  (setq doom-modeline-modal-icon t)
  (setq doom-modeline-buffer-name t)

  ;; Activate doom-modeline
  (doom-modeline-mode 1)

  ;; Set custom modeline format as default
  (defun setup-custom-doom-modeline ()
    (doom-modeline-set-modeline 'hiro/modeline 'default))

  (add-hook 'doom-modeline-mode-hook #'setup-custom-doom-modeline)

  ;; Custom modal icons using ef-themes colors
  (with-eval-after-load 'doom-modeline
    ;; Evil state to text mapping
    (defconst my/evil-state-alist
      '((normal   . " <N> ")
        (insert   . " <I> ")
        (visual   . " <V> ")
        (replace  . " <R> ")
        (operator . " <O> ")
        (motion   . " <M> ")
        (emacs    . " <E> "))
      "Mapping of evil states to display text.")

    ;; Get face attributes for modal indicator
    (defun my/get-modal-face-attrs (active-p)
      "Return face attributes for modal indicator."
      (if active-p
          `(:background ,(ef-themes-get-color-value 'bg-region)
            :foreground ,(ef-themes-get-color-value 'fg-main)
            :weight bold
            :box (:line-width 4 :color ,(ef-themes-get-color-value 'bg-region)))
        `(:background unspecified
          :foreground ,(ef-themes-get-color-value 'fg-dim)
          :weight normal
          :box (:line-width 4 :color ,(ef-themes-get-color-value 'bg-alt)))))

    ;; Define custom modal text function
    (defun my/doom-modeline-modal-text ()
      "Custom modal text using ef-themes colors."
      (when (bound-and-true-p evil-mode)
        (let* ((text (or (alist-get evil-state my/evil-state-alist) " ? "))
               (attrs (my/get-modal-face-attrs (doom-modeline--active))))
          (propertize text 'face attrs))))

    ;; Override the doom-modeline modal segment
    (doom-modeline-def-segment modals
      "Display modal editing states."
      (when doom-modeline-modal
        (my/doom-modeline-modal-text)))

    ;; Clean up other modeline faces
    (set-face-attribute 'doom-modeline-buffer-file nil
                        :weight 'normal)
    (set-face-attribute 'doom-modeline-buffer-major-mode nil
                        :foreground nil :weight 'normal))

  (straight-use-package 'recursion-indicator)
  (setq recursion-indicator-general "&")
  (setq recursion-indicator-minibuffer "@")
  (recursion-indicator-mode 1)
#+end_src
